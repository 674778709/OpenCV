apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 27

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 21
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }
}

version = PROJ_VERSION
group = PROJ_GROUP
project.archivesBaseName=PROJ_ARTIFACTID
//打包javadocjar和sourcejar
task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {
    //增加编码，这行很重要，如果是windows系统，同时如果在代码中有中文的话，不加这行，上传会报错
    options.encoding="utf-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
tasks.getByPath(":app:javadoc").enabled = false
//javadoc {
//    options{
//        encoding "UTF-8"
//        charSet 'UTF-8'
//        author true
//        version true
//        links PROJ_WEBSITEURL
//        title "$PROJ_NAME $PROJ_VERSION"
//    }
//}

artifacts {
    archives sourcesJar
    archives javadocJar
}

//定义pom并打包aar
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name PROJ_NAME
                description PROJ_DESCRIPTION
                url PROJ_WEBSITEURL
                inceptionYear '2018'

                packaging 'aar'
                groupId PROJ_GROUP
                artifactId PROJ_ARTIFACTID
                version PROJ_VERSION

                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                scm {
                    connection PROJ_VCSURL
                    url PROJ_WEBSITEURL

                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
            }
        }
    }
}

//配置bintray参数
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    dryRun = false
    publish = true
    pkg {
        repo = PROJ_REPO               //跟上面创建的Maven仓库名字保持一致
        name = PROJ_PACKNAME              //发布到JCenter上的项目名字
        websiteUrl = PROJ_WEBSITEURL
        vcsUrl = PROJ_VCSURL
        issueTrackerUrl = PROJ_ISSUETRACKERURL
        licenses = ["Apache-2.0"]
        publish = true
        publicDownloadNumbers = true
        version {
            name = PROJ_VERSION
            desc = PROJ_DESCRIPTION
            vcsTag = PROJ_VERSION
            gpg {
                sign = true
            }
        }
    }
}